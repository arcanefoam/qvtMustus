/**
 * <copyright>
 *
 * Copyright (c) 2010 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * </copyright>
 *
 * $Id: GenerateXtextModels.mwe2,v 1.2 2011/01/24 20:54:27 ewillink Exp $
 * 
 * Run As->MWE2 Workflow to regenerate the Xtest CST models.
 */
module GenerateQVTXtextModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the Concrete Syntax Models used by the Xtext editors.
 */
Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = ".."
//    	platformUri = "./META-INF"		// A local folder to minimize workspace searching
    	scanClassPath = true			// But we do need to search the plugin-space
    	registerGeneratedEPackage = "org.eclipse.xtext.common.types.TypesPackage"
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.emf.ecore/"
    		to = "platform:/resource/org.eclipse.emf.ecore/"
    	}
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.ocl.examples.pivot/"
    		to = "platform:/resource/org.eclipse.ocl.examples.pivot/"
    	}
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.ocl.examples.xtext.base/"
    		to = "platform:/resource/org.eclipse.ocl.examples.xtext.base/"
    	}
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.ocl.examples.xtext.essentialocl/"
    		to = "platform:/resource/org.eclipse.ocl.examples.xtext.essentialocl/"
    	}
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.qvtd.pivot.qvtbase/"
    		to = "platform:/resource/org.eclipse.qvtd.pivot.qvtbase/"
    	}
    	uriMap = Mapping {
    		from = "platform:/plugin/org.eclipse.xtext.common.types/"
    		to = "platform:/resource/org.eclipse.xtext.common.types/"
    	}
    }
    bean = GenModelSetup { resourceSet = resourceSet }
    bean = UMLGenModelSetup { resourceSet = resourceSet }
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/org.eclipse.qvtd.xtext.qvtcore/model/QVTcoreCST.genmodel" 	
    }
    component = XtextVisitorCodeGenerator { resourceSet = resourceSet
    	ecoreFile = "/org.eclipse.qvtd.xtext.qvtcore/model/QVTcoreCST.ecore"
    	javaFolder = "/../plugins/org.eclipse.qvtd.xtext.qvtcore/emf-gen"
    	javaPackageName = "org.eclipse.qvtd.xtext.qvtcorecst"
    	modelPackageName = "org.eclipse.qvtd.xtext.qvtcorecst"
    	visitorClassName = "QVTcoreCSVisitor"
    	visitorBasePackageName = "org.eclipse.ocl.examples.xtext.essentialocl.util"
    	visitorBaseClassName = "EssentialOCLCSVisitor"
    }
}

